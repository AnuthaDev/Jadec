/*
 //noinspection GradleDependency
 * Show Java - A java/apk decompiler for android
 * Copyright (c) 2018 Niranjan Rajendran
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "com.google.android.gms.oss-licenses-plugin"
//apply plugin: "io.michaelrocks.paranoid"
apply plugin: "com.google.gms.google-services"
apply plugin: "ru.tinkoff.gradle.jarjar"

def versionMajor = 3
def versionMinor = 0
def versionPatch = 6
def versionBuild = 4

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isCi = "true" == System.getenv("CI")
def preDexEnabled = "true" == System.getProperty("PRE_DEX", "true")

def props = new Properties()
try {
    props.load(new FileInputStream("app/keys.properties"))
} catch (e) {
    props.load(new FileInputStream("app/keys.properties.example"))
    println("===========")
    println(e.message)
    println("Loaded app/keys.properties.example instead.")
    println("===========")
    println()
}

android {
    compileSdkVersion 33
    dexOptions {
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        multiDexEnabled true
        multiDexKeepProguard file("multidex-config.pro")

        applicationId "com.thesourceofcode.jadec"
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resourceConfigurations += ['en']

        vectorDrawables.useSupportLibrary true

        proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

        resValue "string", "appName", "Jadec"
        resValue "string", "appNameGetPro", "Get Show Java Pro"
        resValue "string", "appVersion", android.defaultConfig.versionName
        resValue "string", "appVersionExtended", "Version ${android.defaultConfig.versionName}"
        resValue "string", "appVersionExtendedWithHash", "Version ${android.defaultConfig.versionName} (${getGitHash()})"

        resValue "string", "deviceId", props.getProperty("deviceId")
        resValue "string", "adUnitId", props.getProperty("adUnitId")
        resValue "string", "admobAppId", props.getProperty("admobAppId")
        resValue "string", "admobPublisherId", props.getProperty("admobPublisherId")
        resValue "string", "privacyPolicyUrl", props.getProperty("privacyPolicyUrl")

        buildConfigField "String", "GIT_SHA", "\"${getGitHash()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "PLAY_LICENSE_KEY", "\"${props.getProperty("googlePlayLicenseKey")}\""
        buildConfigField "String", "IAP_PRODUCT_ID", "\"${props.getProperty("IapProductId")}\""
        buildConfigField "String", "BACKEND_URL", "\"${props.getProperty("backendUrl")}\""
        buildConfigField "String", "PURCHASE_VERIFIER_PATH", "\"${props.getProperty("purchaseVerifierPath")}\""
        buildConfigField "Boolean", "IS_CI", "${isCi}"
        buildConfigField "Boolean", "EXTENDED_VALIDATION", "false"

        manifestPlaceholders = [admobAppId: props.getProperty("admobAppId"), crashlyticsApiKey: props.getProperty("crashlyticsApiKey")]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/MANIFEST.MF', 'META-INF/rxjava.properties', 'resources.arsc']
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }


    flavorDimensions "type"

    productFlavors {
        standard {
            dimension = "type"
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            buildConfigField "Boolean", "EXTENDED_VALIDATION", "true"
        }
        debug {
            ext.enableCrashlytics = false
            buildConfigField "Boolean", "EXTENDED_VALIDATION", props.getProperty("debugExtendedValidation", "false")
            minifyEnabled props.getProperty("minifyEnabled", "true") == "true"
            shrinkResources props.getProperty("shrinkResources", "true") == "true"
            testProguardFile "test-proguard-rules.pro"
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        main.resources.srcDirs += "src/main/config"
        test.java.srcDirs += "src/test/kotlin"
        androidTest.java.srcDirs += "src/androidTest/kotlin"
    }
    lint {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'com.thesourceofcode.jadec'
}

configurations.all {
    resolutionStrategy {
        force "com.google.code.findbugs:jsr305:2.0.3"
    }
}

dependencies {
    // Multi-dex
    implementation "androidx.multidex:multidex:2.0.1"

    // Kotlin Standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Firebase-related dependencies
    implementation "com.google.firebase:firebase-core:21.1.1"
    implementation 'com.google.firebase:firebase-ads:22.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.3.6'
    implementation 'com.google.firebase:firebase-analytics:21.2.2'

    // AndroidX support libraries
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "com.google.android.material:material:1.8.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation 'androidx.core:core:1.10.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation "com.google.android.ads.consent:consent-library:1.0.8"
    implementation "com.android.volley:volley:1.2.1"

    // Android WorkManager
    implementation "androidx.work:work-runtime-ktx:2.8.1"



    // Dex2Jar & friends
    implementation "asm:asm-all:3.3.1"
    implementation 'org.smali:dexlib2:2.5.2'
    implementation files("libs/dex-ir-1.12.jar")
    implementation files("libs/dex-reader-1.15.jar")
    implementation files("libs/dex-tools-0.0.9.15.jar")
    implementation files("libs/dex-translator-0.0.9.15.jar")

    // CFR Decompiler
    implementation 'org.benf:cfr:0.152'


    // DX Tool. We are using jar-jar to repackage the dx lib.
    // As android comes with the same classes in the lib-art.jar package that is usually a tad bit
    // older than what we need and hence missing stuff.
    jarJar files("libs/dx-1.16.jar")

    // JaDX & friends
    implementation files("libs/jadx-core-0.9.0.jar")
    implementation files("libs/dx-1.16.jar")
    implementation files("libs/android-5.1.jar")
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-simple:1.7.26'

    // Required by JADX
    implementation 'uk.com.robust-it:cloning:1.9.11'
    // Fix version to 1.9.11. Update to 1.9.12 causes compilation to fail due to org.objenesis:3.0.1
    // implementation "ch.qos.logback:logback-classic:1.2.3"
    // implementation "ch.qos.logback:logback-core:1.2.3"

    // APK parser
    implementation 'net.dongliu:apk-parser:2.6.9'

    // Fernflower decompiler
    implementation "com.github.fesh0r:fernflower:a0a8f0a8dd"

    // RxAndroid & friends
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"

    // Testing tools
    testImplementation "junit:junit:4.12"
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: "com.android.support", module: "support-annotations"
        exclude group: "com.google.code.findbugs", module: "jsr305"
    })

    // Everybody else
    //noinspection GradleDependency
    implementation "commons-io:commons-io:2.5" // Fix to 2.5 for compatibility
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation('org.solovyev.android:checkout:1.2.1', {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    })
    implementation "pub.devrel:easypermissions:3.0.0"
    implementation("org.solovyev.android:checkout:1.2.1", {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    })
    implementation "com.github.kobakei:MaterialFabSpeedDial:2.0.0"
    implementation "com.daimajia.numberprogressbar:library:1.4@aar"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view:3.10.0"
    implementation "com.github.angads25:filepicker:1.1.1"
    implementation "com.github.javiersantos:PiracyChecker:1.2.5"
    implementation 'com.scottyab:secure-preferences-lib:0.1.7'
    implementation "com.github.nekocode:Badge:2.1"

    implementation fileTree(dir: "build/libs", include: ["*.jar"])
}

jarJar {
    rules = ["dx-1.16.jar": "com.android.** xyz.codezero.android.@1"]
}

apply plugin: "hiBeaver"

import com.bryansharp.gradle.hibeaver.utils.MethodLogAdapter
import org.objectweb.asm.ClassVisitor
import org.objectweb.asm.MethodVisitor
import org.objectweb.asm.Opcodes

hiBeaver {
    hiBeaverModifyName = "showJavaDexModifications"
    showHelp = false
    keepQuiet = false
    watchTimeConsume = false

    modifyMatchMaps = [
            /*
             * JaDX does not log class-by-class progress updates.
             * So we patch the the method that saves the class at the bytecode level
             * to invoke a log method at com.thesourceofcode.jadec.Logger
             *
             * Note: this needs to be tested and adjusted for every update of JaDX.
             */
            "jadx.core.dex.visitors.SaveCode": [
                    ["methodName": "save", "methodDesc": "(Ljava/io/File;Ljadx/api/JadxArgs;Ljadx/core/dex/nodes/ClassNode;)V", "adapter": {
                        ClassVisitor cv, int access, String name, String desc, String signature, String[] exceptions ->
                            MethodVisitor methodVisitor = cv.visitMethod(access, name, desc, signature, exceptions)
                            MethodVisitor adapter = new MethodLogAdapter(methodVisitor) {
                                @Override
                                void visitCode() {
                                    super.visitCode()
                                    methodVisitor.visitVarInsn(Opcodes.ALOAD, 0)
                                    methodVisitor.visitVarInsn(Opcodes.ALOAD, 1)
                                    methodVisitor.visitVarInsn(Opcodes.ALOAD, 2)
                                    methodVisitor.visitMethodInsn(
                                            Opcodes.INVOKESTATIC,
                                            className2Path("com.thesourceofcode.jadec.utils.streams.Logger"),
                                            "logJadxClassWrite",
                                            "(Ljava/io/File;Ljadx/api/JadxArgs;Ljadx/core/dex/nodes/ClassNode;)V",
                                            false
                                    )
                                }
                            }
                            return adapter
                    }]
            ],
            /*
             * JaDX uses java.nio.* classes for checking if the file system is case sensitive or not.
             * But java.nio.* classes are not available on on Android API < 26.
             * So, we will patch the method to return true thereby skipping all java.nio.* usages.
             *
             * Note: this needs to be tested and adjusted for every update of JaDX.
             */
            "jadx.core.utils.files.FileUtils": [
                    ["methodName": "isCaseSensitiveFS", "methodDesc": "(Ljava/io/File;)Z", "adapter": {
                        ClassVisitor cv, int access, String name, String desc, String signature, String[] exceptions ->
                            MethodVisitor methodVisitor = cv.visitMethod(access, name, desc, signature, exceptions)
                            MethodVisitor adapter = new MethodLogAdapter(methodVisitor) {
                                @Override
                                void visitCode() {
                                    methodVisitor.visitCode()
                                    methodVisitor.visitInsn(Opcodes.ICONST_1)
                                    methodVisitor.visitInsn(Opcodes.IRETURN)
                                    methodVisitor.visitEnd()
                                }
                            }
                            return adapter
                    }]
            ]
    ]
}
